import os
Import('*')

bin_list = [
			("essfwi", "main-essfwi2d.cpp"),
			("essfwi-enq", "main-essfwi-enquist2d.cpp"),
			("essfwi-new", "main-essfwi2d-new.cpp"),
			("enfwi", "main-enfwi.cpp"),
			("enfwi-regu", "main-enfwi-regu.cpp"),
			("essfwi-sponge", "main-essfwi-sponge.cpp"),
			("essfwi-sponge-grad", "main-essfwi-sponge-grad.cpp"),

           ]

lib = 'essfwi'

modules = """
essfwi-params.cpp
essfwiframework.cpp
Matrix.cpp
calgainmatrix.cpp
dgesvd.cpp
essfwiregu.cpp
ReguFactor.cpp
updatevelop.cpp
updatesteplenop.cpp
          """.split()

include_dir = [
  os.environ['RSFROOT'] + '/include',
  '#' + dirs['util'],
  '#' + dirs['common'],
  '#' + dirs['modeling'],
  '#' + dirs['mdlib'],
]

depend_libpath = [os.environ['RSFROOT'] + '/lib']
depend_libs = ['modeling', 'mdlib', 'common', 'rsf', 'util']

# clone the environment from global env and make some modification of it
myenv   = env.Clone(CPPPATH = include_dir)
myenv.Append(LIBPATH = depend_libpath)
myenv.Append(CXXFLAGS = '-DNO_BLAS')
myenv.Append(LIBS = depend_libs)

objs      = myenv.StaticObject(modules)

# compile the library
ar = myenv.StaticLibrary(target = lib, source = objs)

# install
myenv.Install("#" + dirs['lib'], ar)

# compile each binary
for bin in bin_list:
  bin_obj = myenv.StaticObject(bin[1])
  bin_target = myenv.Program(target = bin[0], source = bin_obj + ar)
  myenv.Install("#" + dirs['bin'], bin_target)
